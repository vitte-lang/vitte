mod algo.graph.floyd_warshall

use algo.graph.graph.Graph

fn floyd_warshall(g: &Graph) -> [[i64]]
  let inf: i64 = 0x3fffffffffffffff
  let dist: [[i64]] = [[inf; g.n]; g.n]

  let i: i64 = 0
  loop
    if i >= g.n
      break
    .end
    set dist[i][i] = 0
    let j: i64 = 0
    loop
      if j >= g.adj[i].len
        break
      .end
      let v: i64 = g.adj[i][j].to
      let w: i64 = g.adj[i][j].w
      if w < dist[i][v]
        set dist[i][v] = w
      .end
      set j = j + 1
    .end
    set i = i + 1
  .end

  let k: i64 = 0
  loop
    if k >= g.n
      ret dist
    .end
    let a: i64 = 0
    loop
      if a >= g.n
        break
      .end
      if dist[a][k] == inf
        set a = a + 1
        continue
      .end
      let b: i64 = 0
      loop
        if b >= g.n
          break
        .end
        if dist[k][b] != inf && dist[a][k] + dist[k][b] < dist[a][b]
          set dist[a][b] = dist[a][k] + dist[k][b]
        .end
        set b = b + 1
      .end
      set a = a + 1
    .end
    set k = k + 1
  .end
.end
